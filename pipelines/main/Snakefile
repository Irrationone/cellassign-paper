
configfile: 'config/v3.yaml'

singularity: "docker://alzhang/scrna-analysis-main-3.5:v1.5"

rule all:
    input:
        ## Main figures
        fl_overview_figure='{outdir}/figures/fl_overview_figure.pdf'.format(outdir=config['outdir']),
        sim_performance_figure='{outdir}/figures/sim_performance_figure.pdf'.format(outdir=config['outdir']),
        fl_nonmalignant_figure='{outdir}/figures/fl_nonmalignant_figure.pdf'.format(outdir=config['outdir']),
        fl_malignant_figure='{outdir}/figures/fl_malignant_figure.pdf'.format(outdir=config['outdir']),
        ## Supplemental figures
        sim_performance_b_cd8='{outdir}/supplemental_figures/sim_performance_b_cd8.pdf'.format(outdir=config['outdir']),
        wrong_marker_figure='{outdir}/supplemental_figures/wrong_marker_naivecd8_naivecd4.pdf'.format(outdir=config['outdir']),
        real_data_evaluation_figure='{outdir}/supplemental_figures/real_data_evaluation.pdf'.format(outdir=config['outdir']),
        fl_markers_figure='{outdir}/supplemental_figures/fl_markers_supplemental.pdf'.format(outdir=config['outdir']),
        light_chain_figure='{outdir}/supplemental_figures/light_chain_heatmap.pdf'.format(outdir=config['outdir']),
        rln_markers_figure='{outdir}/supplemental_figures/rln_markers.pdf'.format(outdir=config['outdir']),
        fl_de_malignant_figure='{outdir}/supplemental_figures/fl_de_malignant_nonmalignant.pdf'.format(outdir=config['outdir']),
        fl_nonmalignant_timepoint_network_plot='{outdir}/supplemental_figures/fl_nonmalignant_timepoint.pdf'.format(outdir=config['outdir']),
        fl_hla_downregulation_plot='{outdir}/supplemental_figures/fl_hla_downregulation.pdf'.format(outdir=config['outdir']),
        ## Supplemental tables
        simulation_performance_table='{outdir}/supplemental_tables/simulation_performance_table.xlsx'.format(outdir=config['outdir']),
        marker_gene_matrix_table='{outdir}/supplemental_tables/marker_gene_matrix_table.xlsx'.format(outdir=config['outdir']),
        pathway_enrichment_table='{outdir}/supplemental_tables/pathway_enrichment_table.xlsx'.format(outdir=config['outdir']),
        ## Stats
        stats='{outdir}/stats/stats.json'.format(outdir=config['outdir']),


## Main figures

rule simulation_performance_figure:
    input:
        deprob_result_dir=config['simulation_analysis_results']['deprob_result_dir'],
        wrongmarker_result_dir=config['simulation_analysis_results']['wrongmarker_result_dir'],
    output:
        sim_performance_figure='{outdir}/figures/sim_performance_figure.pdf'.format(outdir=config['outdir'])
    params:
        name='simulation-performance-figure',
        delta_deprobs=config['delta_deprobs'],
        deprob_methods=config['deprob_methods'],
    log:
        '{logdir}/logs/simulation_performance_figure.log'.format(logdir=config['logdir'])
    benchmark:
        '{logdir}/benchmarks/simulation_performance_figure.txt'.format(logdir=config['logdir'])
    shell:
        'Rscript R/main_figures/simulation_overview_figure.R '
        '--deprob_result_dir {input.deprob_result_dir} '
        '--wrongmarker_result_dir {input.wrongmarker_result_dir} '
        '--delta_deprobs {params.delta_deprobs} '
        '--deprob_methods {params.deprob_methods} '
        '--outfname {output.sim_performance_figure} '
        '>& {log}'

rule fl_overview_figure:
    input:
        sce=config['follicular_analysis_results']['sce_annotated'],
        sce_raw=config['follicular_analysis_results']['sce_raw'],
        patient_events=config['follicular_metadata']['patient_progression_file'],
    output:
        fl_overview_figure='{outdir}/figures/fl_overview_figure.pdf'.format(outdir=config['outdir'])
    params:
        name='fl-overview-figure',
        dimreduce_type=config['plot_dimreduce_type'],
        expr_threshold=config['winsorized_expression']['markers'],
    log:
        '{logdir}/logs/fl_overview_figure.log'.format(logdir=config['logdir'])
    benchmark:
        '{logdir}/benchmarks/fl_overview_figure.txt'.format(logdir=config['logdir'])
    shell:
        'Rscript R/main_figures/fl_overview_figure.R '
        '--sce {input.sce} '
        '--sce_raw {input.sce_raw} '
        '--patient_progression {input.patient_events} '
        '--dimreduce_type {params.dimreduce_type} '
        '--winsorized_expression_threshold {params.expr_threshold} '
        '--outfname {output.fl_overview_figure} '
        '>& {log}'


rule fl_nonmalignant_figure:
    input:
        sce=config['follicular_analysis_results']['sce_annotated'],
        sce_tcell=config['follicular_analysis_results']['sce_tcell_annotated'],
        sce_bcell=config['follicular_analysis_results']['sce_bcell_annotated'],
        scvis_merged=config['follicular_analysis_results']['scvis_merged'],
        de_cytotoxic=config['follicular_analysis_results']['differential_expression']['cytotoxic'],
        azizi_signatures=config['external']['azizi_s4'],
    output:
        fl_nonmalignant_figure='{outdir}/figures/fl_nonmalignant_figure.pdf'.format(outdir=config['outdir'])
    params:
        name='fl-timepoint-figure',
        dimreduce_type=config['plot_dimreduce_type'],
        tcell_labels=config['follicular_celltype_labels']['tcell'],
        bcell_labels=config['follicular_celltype_labels']['bcell'],
        expr_threshold=config['winsorized_expression']['lambda_kappa'],
    log:
        '{logdir}/logs/fl_nonmalignant_figure.log'.format(logdir=config['logdir'])
    benchmark:
        '{logdir}/benchmarks/fl_nonmalignant_figure.txt'.format(logdir=config['logdir'])
    shell:
        'Rscript R/main_figures/fl_nonmalignant_figure.R '
        '--sce {input.sce} '
        '--sce_tcell {input.sce_tcell} '
        '--sce_bcell {input.sce_bcell} '
        '--sce_scvis_merged {input.scvis_merged} '
        '--tcell_labels {params.tcell_labels} '
        '--bcell_labels {params.bcell_labels} '
        '--azizi_signatures {input.azizi_signatures} '
        '--de_cytotoxic {input.de_cytotoxic} '
        '--dimreduce_type {params.dimreduce_type} '
        '--winsorized_expression_threshold {params.expr_threshold} '
        '--outfname {output.fl_nonmalignant_figure} '
        '>& {log}'

rule fl_malignant_figure:
    input:
        sce=config['follicular_analysis_results']['sce_annotated'],
        sce_bcell=config['follicular_analysis_results']['sce_bcell_annotated'],
        de_timepoint=config['follicular_analysis_results']['differential_expression']['timepoint'],
        de_timepoint_fgsea=config['follicular_analysis_results']['differential_expression']['timepoint_fgsea'],
        de_malignant_timepoint=config['follicular_analysis_results']['differential_expression']['malignant_timepoint'],
    output:
        fl_malignant_figure='{outdir}/figures/fl_malignant_figure.pdf'.format(outdir=config['outdir'])
    params:
        name='fl-timepoint-figure',
        bcell_labels=config['follicular_celltype_labels']['bcell'],
        expr_threshold=config['winsorized_expression']['proliferation'],
    log:
        '{logdir}/logs/fl_malignant_figure.log'.format(logdir=config['logdir'])
    benchmark:
        '{logdir}/benchmarks/fl_malignant_figure.txt'.format(logdir=config['logdir'])
    shell:
        'Rscript R/main_figures/fl_malignant_figure.R '
        '--sce {input.sce} '
        '--sce_bcell {input.sce_bcell} '
        '--bcell_labels {params.bcell_labels} '
        '--de_timepoint_dir {input.de_timepoint} '
        '--de_timepoint_fgsea_dir {input.de_timepoint_fgsea} '
        '--de_malignant_timepoint_dir {input.de_malignant_timepoint} '
        '--winsorized_expression_threshold {params.expr_threshold} '
        '--outfname {output.fl_malignant_figure} '
        '>& {log}'


## Supplemental figures

rule simulation_performance_b_cd8:
    input:
        deprob_result_dir=config['simulation_analysis_results']['deprob_result_b_cd8_dir'],
    output:
        sim_performance_b_cd8='{outdir}/supplemental_figures/sim_performance_b_cd8.pdf'.format(outdir=config['outdir'])
    params:
        name='suppfigure-simulation-performance-b-cd8',
        delta_deprobs=config['delta_deprobs_supp'],
        deprob_methods=config['deprob_methods'],
    log:
        '{logdir}/logs/simulation_performance_b_cd8.log'.format(logdir=config['logdir'])
    benchmark:
        '{logdir}/benchmarks/simulation_performance_b_cd8.txt'.format(logdir=config['logdir'])
    shell:
        'Rscript R/supplemental_figures/simulation_b_cd8.R '
        '--deprob_result_dir {input.deprob_result_dir} '
        '--delta_deprobs {params.delta_deprobs} '
        '--deprob_methods {params.deprob_methods} '
        '--outfname {output.sim_performance_b_cd8} '
        '>& {log}'

rule wrong_marker_naivecd8_naivecd4:
    input:
        wrongmarker_result_dir=config['simulation_analysis_results']['wrongmarker_result_naivecd8_naivecd4_dir'],
    output:
        wrong_marker_figure='{outdir}/supplemental_figures/wrong_marker_naivecd8_naivecd4.pdf'.format(outdir=config['outdir'])
    params:
        name='suppfigure-wrong-marker-naivecd8-naivecd4',
    log:
        '{logdir}/logs/wrong_marker_naivecd8_naivecd4.log'.format(logdir=config['logdir'])
    benchmark:
        '{logdir}/benchmarks/wrong_marker_naivecd8_naivecd4.txt'.format(logdir=config['logdir'])
    shell:
        'Rscript R/supplemental_figures/wrong_marker_naive_cd4_naive_cd8.R '
        '--wrongmarker_result_dir {input.wrongmarker_result_dir} '
        '--outfname {output.wrong_marker_figure} '
        '>& {log}'


rule real_data_evaluation:
    input:
        koh_annotated=config['external_analysis_results']['koh_annotated'],
    output:
        real_data_evaluation_figure='{outdir}/supplemental_figures/real_data_evaluation.pdf'.format(outdir=config['outdir'])
    params:
        name='suppfigure-real-data-evaluation',
        dimreduce_type='TSNE',
    log:
        '{logdir}/logs/real_data_evaluation.log'.format(logdir=config['logdir'])
    benchmark:
        '{logdir}/benchmarks/real_data_evaluation.txt'.format(logdir=config['logdir'])
    shell:
        'Rscript R/supplemental_figures/cellassign_real_data_evaluation.R '
        '--koh_annotated {input.koh_annotated} '
        '--dimreduce_type {params.dimreduce_type} '
        '--outfname {output.real_data_evaluation_figure} '
        '>& {log}'

rule fl_markers_supplemental:
    input:
        sce=config['follicular_analysis_results']['sce_annotated'],
        cellassign_specific_result=config['follicular_analysis_results']['cellassign_specific_result'],
    output:
        fl_markers_figure='{outdir}/supplemental_figures/fl_markers_supplemental.pdf'.format(outdir=config['outdir'])
    params:
        name='suppfigure-fl-markers',
        expr_threshold=config['winsorized_expression']['markers'],
    log:
        '{logdir}/logs/fl_markers_supplemental.log'.format(logdir=config['logdir'])
    benchmark:
        '{logdir}/benchmarks/fl_markers_supplemental.txt'.format(logdir=config['logdir'])
    shell:
        'Rscript R/supplemental_figures/fl_markers_supplemental.R '
        '--sce {input.sce} '
        '--winsorized_expression_threshold {params.expr_threshold} '
        '--cellassign_results {input.cellassign_specific_result} '
        '--outfname {output.fl_markers_figure} '
        '>& {log}'

rule light_chain_constant_region_heatmap:
    input:
        sce_nonmalignant_b=config['follicular_analysis_results']['sce_nonmalignant_bcell_annotated'],
        cellassign_lambda_kappa_result=config['follicular_analysis_results']['cellassign_lambda_kappa_result'],
    output:
        light_chain_figure='{outdir}/supplemental_figures/light_chain_heatmap.pdf'.format(outdir=config['outdir'])
    params:
        name='suppfigure-light-chain-heatmap',
    log:
        '{logdir}/logs/light_chain_constant_region_heatmap.log'.format(logdir=config['logdir'])
    benchmark:
        '{logdir}/benchmarks/light_chain_constant_region_heatmap.txt'.format(logdir=config['logdir'])
    shell:
        'Rscript R/supplemental_figures/light_chain_constant_region.R '
        '--sce_nonmalignant_bcell {input.sce_nonmalignant_b} '
        '--cellassign_lambda_kappa {input.cellassign_lambda_kappa_result} '
        '--outfname {output.light_chain_figure} '
        '>& {log}'

rule rln_markers:
    input:
        sce=config['follicular_analysis_results']['scvis_merged'],
    output:
        rln_markers_figure='{outdir}/supplemental_figures/rln_markers.pdf'.format(outdir=config['outdir'])
    params:
        name='suppfigure-rln-markers',
        expr_threshold=config['winsorized_expression']['markers'],
    log:
        '{logdir}/logs/rln_markers.log'.format(logdir=config['logdir'])
    benchmark:
        '{logdir}/benchmarks/rln_markers.txt'.format(logdir=config['logdir'])
    shell:
        'Rscript R/supplemental_figures/rln_markers.R '
        '--sce {input.sce} '
        '--winsorized_expression_threshold {params.expr_threshold} '
        '--outfname {output.rln_markers_figure} '
        '>& {log}'

rule fl_de_malignant_nonmalignant_bcells:
    input:
        de_malignant_timepoint=config['follicular_analysis_results']['differential_expression']['malignant_timepoint'],
    output:
        fl_de_malignant_figure='{outdir}/supplemental_figures/fl_de_malignant_nonmalignant.pdf'.format(outdir=config['outdir'])
    params:
        name='suppfigure-fl-de-malignant-nonmalignant-bcells',
    log:
        '{logdir}/logs/fl_de_malignant_nonmalignant_bcells.log'.format(logdir=config['logdir'])
    benchmark:
        '{logdir}/benchmarks/fl_de_malignant_nonmalignant_bcells.txt'.format(logdir=config['logdir'])
    shell:
        'Rscript R/supplemental_figures/fl_de_malignant_vs_nonmalignant_bcells.R '
        '--de_malignant_timepoint_dir {input.de_malignant_timepoint} '
        '--outfname {output.fl_de_malignant_figure} '
        '>& {log}'

rule fl_nonmalignant_timepoint_pathway:
    input:
        de_timepoint=config['follicular_analysis_results']['differential_expression']['timepoint'],
    output:
        fl_nonmalignant_timepoint_network_plot='{outdir}/supplemental_figures/fl_nonmalignant_timepoint.pdf'.format(outdir=config['outdir'])
    params:
        name='suppfigure-fl-nonmalignant-timepoint-pathway',
    log:
        '{logdir}/logs/fl_nonmalignant_timepoint_pathway.log'.format(logdir=config['logdir'])
    benchmark:
        '{logdir}/benchmarks/fl_nonmalignant_timepoint_pathway.txt'.format(logdir=config['logdir'])
    shell:
        'Rscript R/supplemental_figures/fl_nonmalignant_timepoint_pathway.R '
        '--de_timepoint_dir {input.de_timepoint} '
        '--outfname {output.fl_nonmalignant_timepoint_network_plot} '
        '>& {log}'


rule fl_hla_downregulation:
    input:
        de_timepoint=config['follicular_analysis_results']['differential_expression']['timepoint'],
    output:
        fl_hla_downregulation_plot='{outdir}/supplemental_figures/fl_hla_downregulation.pdf'.format(outdir=config['outdir'])
    params:
        name='suppfigure-fl-hla-downregulation',
    log:
        '{logdir}/logs/fl_hla_downregulation.log'.format(logdir=config['logdir'])
    benchmark:
        '{logdir}/benchmarks/fl_hla_downregulation.txt'.format(logdir=config['logdir'])
    shell:
        'Rscript R/supplemental_figures/fl_hla_downregulation.R '
        '--de_timepoint_dir {input.de_timepoint} '
        '--outfname {output.fl_hla_downregulation_plot} '
        '>& {log}'


## Supplemental Tables

rule simulation_performance_table:
    input:
        deprob_result_dir_naivecd8_naivecd4=config['simulation_analysis_results']['deprob_result_dir'],
        deprob_result_dir_b_cd8=config['simulation_analysis_results']['deprob_result_b_cd8_dir'],
        wrongmarker_result_dir_naivecd8_naivecd4=config['simulation_analysis_results']['wrongmarker_result_naivecd8_naivecd4_dir'],
        wrongmarker_result_dir_b_cd8=config['simulation_analysis_results']['wrongmarker_result_dir'],
    output:
        simulation_performance_table='{outdir}/supplemental_tables/simulation_performance_table.xlsx'.format(outdir=config['outdir']),
    params:
        name='supptable-simulation-performance-table',
        deprob_methods=config['deprob_methods'],
        eval_measures=config['eval_measures_to_report'],
    log:
        '{logdir}/logs/simulation_performance_table.log'.format(logdir=config['logdir'])
    benchmark:
        '{logdir}/benchmarks/simulation_performance_table.txt'.format(logdir=config['logdir'])
    shell:
        'Rscript R/supplemental_tables/simulation_performance_table.R '
        '--deprob_result_dir_naivecd8_naivecd4 {input.deprob_result_dir_naivecd8_naivecd4} '
        '--deprob_result_dir_b_cd8 {input.deprob_result_dir_b_cd8} '
        '--wrongmarker_result_dir_naivecd8_naivecd4 {input.wrongmarker_result_dir_naivecd8_naivecd4} '
        '--wrongmarker_result_dir_b_cd8 {input.wrongmarker_result_dir_b_cd8} '
        '--deprob_methods {params.deprob_methods} '
        '--eval_measures {params.eval_measures} '
        '--outfname {output.simulation_performance_table} '
        '>& {log}'

# Marker gene matrices for follicular, Koh
rule marker_gene_matrices_table:
    input:
        koh_rho=config['external_analysis_results']['koh_rho'],
        follicular_gene_list_specific=config['follicular_analysis_settings']['marker_lists']['big']['path'],
        follicular_gene_list_lambda_kappa=config['follicular_analysis_settings']['marker_lists']['lambdakappa']['path'],
    output:
        marker_gene_matrix_table='{outdir}/supplemental_tables/marker_gene_matrix_table.xlsx'.format(outdir=config['outdir']),
    params:
        name='supptable-marker-gene-matrices',
        follicular_specific_other=config['follicular_analysis_settings']['marker_lists']['big']['include_other'],
        follicular_lk_other=config['follicular_analysis_settings']['marker_lists']['lambdakappa']['include_other'],
    log:
        '{logdir}/logs/marker_gene_matrices_table.log'.format(logdir=config['logdir'])
    benchmark:
        '{logdir}/benchmarks/marker_gene_matrices_table.txt'.format(logdir=config['logdir'])
    shell:
        'Rscript R/supplemental_tables/marker_gene_matrices_table.R '
        '--koh_rho {input.koh_rho} '
        '--follicular_gene_list_specific {input.follicular_gene_list_specific} '
        '--follicular_gene_list_lambda_kappa {input.follicular_gene_list_lambda_kappa} '
        '--follicular_specific_other {params.follicular_specific_other} '
        '--follicular_lk_other {params.follicular_lk_other} '
        '--outfname {output.marker_gene_matrix_table} '
        '>& {log}'

# Significantly DE pathways
rule pathway_enrichment_table:
    input:
        de_timepoint=config['follicular_analysis_results']['differential_expression']['timepoint'],
        de_malignant_timepoint=config['follicular_analysis_results']['differential_expression']['malignant_timepoint'],
        de_timepoint_fgsea=config['follicular_analysis_results']['differential_expression']['timepoint_fgsea'],
    output:
        pathway_enrichment_table='{outdir}/supplemental_tables/pathway_enrichment_table.xlsx'.format(outdir=config['outdir']),
    params:
        name='supptable-pathway-enrichment-table',
        padj_threshold=config['supptable_settings']['de']['padj_threshold'],
    log:
        '{logdir}/logs/pathway_enrichment_table.log'.format(logdir=config['logdir'])
    benchmark:
        '{logdir}/benchmarks/pathway_enrichment_table.txt'.format(logdir=config['logdir'])
    shell:
        'Rscript R/supplemental_tables/pathway_enrichment_table.R '
        '--de_timepoint_dir {input.de_timepoint} '
        '--de_malignant_timepoint_dir {input.de_malignant_timepoint} '
        '--de_timepoint_fgsea_dir {input.de_timepoint_fgsea} '
        '--padj_threshold {params.padj_threshold} '
        '--outfname {output.pathway_enrichment_table} '
        '>& {log}'

## Generate stats for the paper

rule compile_stats:
    input:
        sce=config['follicular_analysis_results']['sce_annotated'],
        sce_bcell=config['follicular_analysis_results']['sce_bcell_annotated'],
        sce_raw=config['follicular_analysis_results']['sce_raw'],
        patient_events=config['follicular_metadata']['patient_progression_file'],
        deprob_result_dir_naivecd8_naivecd4=config['simulation_analysis_results']['deprob_result_dir'],
        sce_nonmalignant_b=config['follicular_analysis_results']['sce_nonmalignant_bcell_annotated'],
        cellassign_lambda_kappa_result=config['follicular_analysis_results']['cellassign_lambda_kappa_result'],
        de_timepoint=config['follicular_analysis_results']['differential_expression']['timepoint'],
        de_malignant_timepoint=config['follicular_analysis_results']['differential_expression']['malignant_timepoint'],
        de_timepoint_fgsea=config['follicular_analysis_results']['differential_expression']['timepoint_fgsea'],
        koh_rho=config['external_analysis_results']['koh_rho'],
    output:
        stats='{outdir}/stats/stats.json'.format(outdir=config['outdir']),
    params:
        name='compile-stats',
        delta_deprobs=config['delta_deprobs'],
    log:
        '{logdir}/logs/compile_stats.log'.format(logdir=config['logdir'])
    benchmark:
        '{logdir}/benchmarks/compile_stats.txt'.format(logdir=config['logdir'])
    shell:
        'Rscript R/stats/compile_stats.R '
        '--sce_follicular {input.sce} '
        '--sce_follicular_bcells {input.sce_bcell} '
        '--sce_follicular_raw {input.sce_raw} '
        '--patient_progression {input.patient_events} '
        '--deprob_result_dir {input.deprob_result_dir_naivecd8_naivecd4} '
        '--delta_deprobs {params.delta_deprobs} '
        '--sce_follicular_nonmalignant_bcell {input.sce_nonmalignant_b} '
        '--cellassign_lambda_kappa {input.cellassign_lambda_kappa_result} '
        '--de_timepoint_dir {input.de_timepoint} '
        '--de_malignant_timepoint_dir {input.de_malignant_timepoint} '
        '--de_timepoint_fgsea_dir {input.de_timepoint_fgsea} '
        '--koh_rho {input.koh_rho} '
        '--outfname {output.stats} '
        '>& {log}'
    
